import "../lib.moss" as lib;

assume lib::Ctx;

fn main![Std]() {
  bind lib::ctx();
  let input = lib::input();
  let digits = 12;
  var sum = 0i64;
  var lines = input.lines();
  loop {
    let line = match lines.next() {
      None => break,
      Some(line) => line,
    };
    var joltage = 0i64;
    var start = 0;
    var end = line.length() - digits;
    while end < line.length() {
      var max = 0i64;
      var i = start;
      while i <= end {
        let digit = line.get(i).digit().to_int64();
        if digit > max {
          max = digit;
          start = i + 1;
        }
        i += 1;
      }
      joltage = joltage * 10i64 + max;
      end += 1;
    }
    sum += joltage;
  }
  lib::println(sum.to_string());
}
